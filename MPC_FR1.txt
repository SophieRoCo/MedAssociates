\ Adapted from MEDState Notation Repository - SRC Updated 12/2021

\Before starting user enters values for session time, FR value, reward time, AND WEIGHT IN GRAMS
\When START command is issue the active lever and inactive lever is extended.
\Program will record active lever presses, inactive lever presses, rewards delivered
\Pressing the active lever will result in cocaine infusion (0.75 mg/kg over max 6 s) based on weight and illumination of light above active lever (6s)
\whereas the house light will be illuminated for 20 s, during which pressing the active lever will be counted but will not result cocaine infusions.
\After this 20 s period, the house light will turn off, and the next active lever press will result in cocaine infusion
\Pressing the inactive lever will have no consequences but will be recorded 
\The EspaÃ±a Lab uses this same code for Long Access (360min session), Short Access (120min session), and Seeking tests (60min session)
\For seeking tests, do not connect cocaine syringe as the pump will still operate, serving as an auditory cue. 


\ Inputs
^inactivelever  = 1 \Left Lever
^activelever = 2 \Right Lever

\ Outputs
^leftlever=1\inactive lever
^rightlever=2\active lever
^pump     = 3
^rightlight = 6
^houselight = 7


\ A() = Control Variables with Assigned Aliases as Defined
Var_Alias Session Length (min)            = A(0)  \ Default = 60 minutes
Var_Alias Max Rewards                     = A(1)  \ Default = 100
Var_Alias Fixed Ratio Value               = A(2)  \ Default = 1
Var_Alias Time Out Following Reward (sec) = A(3)  \ Default = 20 seconds
Var_Alias Rat Weight                      = A(5)  \ Default = 250

^Session      = 0
^MaxRewards   = 1
^FRVal        = 2
^TimeOut      = 3
^TimeOutTicks = 4
^Weight       = 5


\ List Data Variables Here

\  B(0) = Total Infusions
\  B(1) = Total Active Lever
\  B(2) = Total Inactive Lever


\  L() = Left  Lever Response Array
\  R() = Right Lever Response Array
\
\  S = Elapsed Time in Session


\ List Working Variables Here
\  C = Ratio Response Counter
\  I = Index into Inactive Lever Response Array L
\  J = Index into Active Lever Response Array R



DIM A = 5
DIM B = 3
DIM L = 10000
DIM R = 10000


\ Z-Pulses Used in this Program
^Z_Reward    = 1   \ Signal Pump Reinforcement
^Z_End       = 32  \ Signal End of Session


\***************************************************
\               Fixed Ratio Schedule
\  S1 - Set Default Values
\       Session Length             (360 minutes)
\       Max Rewards                (100)
\       Fixed Ratio Value          (1)
\       Time Out Following Reward  (20 seconds)
\       Rat Weight                 (250 grams)
\***************************************************
S.S.1,
S1,
  0.01": SET A(^Session) = 360, A(^MaxRewards) = 100;
         SET A(^FRVal)   = 1,  A(^TimeOut)    = 20;
         SET A(^Weight) = 250;
         SET L(I) = -987.987;
         SET R(J) = -987.987 ---> S2

S2,     \ First Statement: Wait for START signal, and
        \ associated stimulus ON.
        \
        \ Second Statement: Update screen display with default values
        \ for Control Variables.  This will show any changes made via
        \ the "Configure | Change Variables" Window prior to START.
  #START: CLEAR 1,200;
          SET A(^TimeOutTicks) = A(^TimeOut) * 1";
          ON ^leftlever, ^rightlever;
          SHOW 1,Session,S/60 ---> S3
  1": SHOW 1,Session Length,A(^Session), 2,Max Rewards,A(^MaxRewards);
      SHOW 3,FR Value,A(^FRVal),         4,Time Out,A(^TimeOut) ---> SX

S3,     \ Time Session Length
  0.01": SET S = S + 0.01;
         SHOW 1,Session,S/60;
         IF S/60 >= A(^Session) [@EndSession, @ContinueTiming]
            @End: Z^Z_End ---> S4
            @Cont: ---> SX
  #Z^Z_End: ---> S4

S4,     \ Wait for Screen Update and end with
        \ STOPABORTFLUSH for Automatic Data Saving
  2": ---> STOPABORTFLUSH


\***************************************************
\                   MAIN PROGRAM
\***************************************************
S.S.2,
S1,
  #START---> S2

S2,     \ Fixed Ratio
  #R^activelever: ADD C;
                IF C >= A(^FRVal) [@FRMet, @False]
                   @FRMet: ADD B(0); SET C = 0; ON ^houselight;
                            Z^Z_Reward ---> S3
                   @False: ---> SX
  #R^inactivelever: ADD B(2) ---> SX
  #Z^Z_End: ---> S4


S3,     \ Time Out Interval Following Reward
  A(^TimeOutTicks)#T: IF B(0) >= A(^MaxRewards) [@End, @Cont]
                         @End: Z^Z_End ---> S4
                         @Cont: Off ^houselight ---> S2
  #Z^Z_End: ---> S4

S4,     \ End of Session - Retract Levers
  0.01": OFF ^leftlever, ^ rightlever ---> S6

S6,     \ Holding State at End of Session
  1': ---> SX


\***************************************************
\             LEFT LEVER RESPONSE ARRAY
\***************************************************
S.S.3,
S1,
  #START: ---> S2

S2,
  #R^inactivelever: ADD B(2); SET L(I) = S, I = I + 1, L(I) = -987.987 ---> SX
  #Z^Z_End: ---> S1


\***************************************************
\            RIGHT LEVER RESPONSE ARRAY
\***************************************************
S.S.4,
S1,
  #START: ---> S2

S2,
  #R^activelever: ADD B(1); SET R(J) = S, J = J + 1, R(J) = -987.987 ---> SX
  #Z^Z_End: ---> S1


\***************************************************
\               REWARD CONTROL TIMER and House Light Time out
\***************************************************
S.S.5,
S1,
  #Z^Z_Reward: ON ^Pump, ^RightLight ---> S2

S2,     \ Time Reward Device for 6" seconds
  (A(^Weight) * 1.125)#T: OFF ^Pump, ^RightLight ---> S1 \ change pump time here... based on WEIGHT IN GRAMS



\***************************************************
\                  UPDATE DISPLAY
\***************************************************
S.S.6,
S1,
  #START: SHOW 3,Active,B(1), 4,Inactive,B(2), 2,Infusions,B(0)---> S2

S2,
0.01": SHOW 3,Active,B(1), 4,Inactive,B(2), 2,Infusions,B(0)---> S2

